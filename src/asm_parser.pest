//! Assembly Parser Grammar
//!
//! This file defines the the gammar to parse some assembly snippets, using the
//! [Pest](https://pest.rs/) PEG parser generator.

WHITESPACE = _{ " " | "\t" }

// Punctuation and parenthesis
l_par = _{ "(" }
r_par = _{ ")" }

// Operators
add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }

// Numerical expressions
var         = @{ (ASCII_ALPHANUMERIC | "_")+ }
hex_number  = @{ "0x" ~ ASCII_HEX_DIGIT+ }
number      = @{ ASCII_DIGIT+ }
unary_minus =  { "-" }
constant    = @{ "const(" ~ var ~ ")" }
binop       = _{ add | sub | mul | div }
primary     = _{ hex_number | number | constant | l_par ~ expr ~ r_par }
atom        = _{ unary_minus? ~ primary }
expr        =  { atom ~ ( binop ~ atom )* }

// Assembly constructs
any_reg = @{ (ASCII_ALPHANUMERIC | "_" | "{" | "}" )+ }
imm_off =  { expr ~ l_par ~ any_reg ~ r_par }

// Assembly line
comment    = _{ "//" ~ ANY* }
label_id   = @{ (ASCII_ALPHANUMERIC | "_")+ }
mnemonic   = @{ ASCII_ALPHANUMERIC+ }
operand    = @{ (!"//" ~ (ASCII_ALPHANUMERIC | "_" | "(" | ")" | "{" | "}" | "+" | "-" | "*" | "/"))+ }
asm_instr  =  { mnemonic ~ (operand ~ ","?)* ~ comment? }
asm_label  =  { label_id ~ ":" }
empty_line =  { WHITESPACE* ~ comment? }
asm_line   =  { WHITESPACE* ~ (asm_label | asm_instr | empty_line) }
